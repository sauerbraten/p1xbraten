//  Standard menu definitions.
//  Don't modify, add personal menus to 'autoexec.cfg' instead.

//  Only for testing.  This will break stuff, delete 'config.cfg' after use.
reui = [exec "data/menus.cfg"; echo "Menus Reinitialized"]

togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]
bind ESCAPE [togglemainmenu]

guirollovername          = ""
guirolloveraction        = ""
guirolloverimgpath       = ""
guirolloverimgaction     = ""
guirolloverpreviewname   = ""
guirolloverpreviewaction = ""
checkrolloveraction = [
    if (>= (strstr $guirolloveraction $arg1) 0) [
        if (>= $numargs 2) [arg2] [result $guirolloveraction]
    ]
]

chooseplayermodel = [guirolloveraction = (playermodelbutton $playermodel); showgui "playermodel"]

newgui "main" [
    guilist [
        guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [chooseplayermodel] 1.15
        guistrut 0.5
        guilist [
            _myname = (getname)
            guifield _myname 15 [name $_myname]
            guispring
            guilist [
                guibutton (playermodelname) [chooseplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui "crosshair"] 0.5
            ]
        ]
    ]
    guistrut 1
    guibutton "server browser.."            [showgui "servers"]
    if (isconnected) [
        guistrut 1
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.."           [showgui "editing"]
        ]
        guibutton "vote game mode / map.."  [showgui "gamemode"]
        // guibutton "lanconnect"
        // guibutton "connect localhost"
        guibutton "switch team"             [if (strcmp (getteam) "good") [team "evil"] [team "good"]]
        guibutton "toggle spectator"        [spectator (! (isspectator (getclientnum)))]                "spectator"
        guibutton "master.."                [showgui "master"]
        guistrut 1
        guibutton "options.."               [showgui "options"]
        guibutton "disconnect"              [disconnect]                                                "exit"
    ] [
        guibutton "bot match.."             [showgui "botmatch"]
        guibutton "campaign.."              [showgui "campaign"]
        guistrut 1
        guibutton "options.."               [showgui "options"]
        guibutton "about.."                 [showgui "about"]
        guibutton "quit"                    [quit]                                                      "exit"
    ]
] 0

newgui about [
    guitext "Cube 2: Sauerbraten" 0
    guitext [by Wouter "Aardappel" van Oortmerssen, Lee "eihrul" Salzman,] 0
    guitext [Mike "Gilt" Dysart, Robert "baby-rabbit" Pointon,] 0
    guitext [John "geartrooper" Siar, Quinton "Quin" Reeves, and others] 0
    guitext "(for a full list of contributors see the readme)" 0
    guitext "http://sauerbraten.org/" 0
]

genmapitems = [
    looplist curmap $arg1 [
        guibutton $curmap (concat map $curmap) "cube"
    ]
]

newgui "about" [
    guitext "  ^f7Cube 2: Sauerbraten" 0
    guitext "    ^f7by Wouter ^"Aardappel^" van Oortmerssen, Lee ^"eihrul^" Salzman,  " 0
    guitext "    ^f7Mike ^"Gilt^" Dysart, Robert ^"baby-rabbit^" Pointon," 0
    guitext "    ^f7John ^"geartrooper^" Siar, Quinton ^"Quin^" Reeves, and others." 0
    guitext "" 0
    guitext "    ^f7(For a full list of contributors see the readme.)" 0
    guitext "    ^f7http://sauerbraten.org/" 0
]

_genmapitems = [
    looplist curmap $arg1 [
        guibutton $curmap (concat map $curmap) "cube"
    ]
]

// 17 per-group.
_maps_ffa1  = "aard3c abyss academy access albatross akaritori akimiski alithia alloy antel anubis aod aqueducts arbana asenatra asthma averas"
_maps_ffa2  = "awoken bvdm_01 carbide cartel castle_trap catacombs cavefire church51 clash collusion colony complex conflict corruption crypta curvedm curvy_castle"
_maps_ffa3  = "darkdeath deathtek depot dirtndust dispute DM_BS1 dock dopamine douze duel5 duel7 duel8 dune elegy exist exo fallen"
_maps_ffa4  = "fanatic_quake fdm6 ferguson force frag-lab frag2 fragplaza frostbyte frozen fubuki fury ghetto gorge gothic-df guacamole gubo hades"
_maps_ffa5  = "hashi hator haze hdm3 headroom helligsted hektik hillfort hog2 horus idyll3 imhotep industry infernal injustice insipid island"
_maps_ffa6  = "janela justice kalking1 kastro katrez_d kffa killfactory kmap5 konkuri-to ksauer1 legacy legazzo lost_soul lost_world lostinspace maple masdm"
_maps_ffa7  = "mbt2 mbt9 mbt10 memento memoria metl2 metl3 metl4 mood moonlite neondevastation neonpanic nessus nmp8 nmp10 nucleus oasis"
_maps_ffa8  = "oddworld ognjen ogrosupply oldschool orbe orion osiris ot outpost paradigm pariah park pgdm phosgene phrantic pitch_black powerplant"
_maps_ffa9  = "purgatory refuge renegade rm1 rm5 roughinery ruby ruine saffier sandstorm sauerowalk sauerstruck sdm1 shadowed shindou shinmei1 shiva"
_maps_ffa10 = "simplicity skrdm1 skycastle-r slingshot souls spcr2 stahlbox stemple stronghold suburb suisei tartech teahupoo tejen thetowers thor torment"
_maps_ffa11 = "toxicity tumwalk turbine turmoil unworld unworld2 ventania waltz wake5 wdcd zamak zavial zdm2 ztn"

_maps_capture1 = "aastha abbey access akimiski akroseum alithia anubis aod arabic asenatra asgard asteroids averas bklyn c_egypt c_lone c_valley"
_maps_capture2 = "campo capture_night caribbean casa collide collusion core_refuge core_transfer corruption croma cwcastle damnation destiny dirtndust disc disruption donya"
_maps_capture3 = "duomo dust2 earthstation eris eternal_valley evilness face-capture fallen fb_capture fc3 fc4 fc5 forge fragnostic frostbyte fusion genesis"
_maps_capture4 = "ghetto gorge gothic-df hades hallo harbor haste hidden imhotep infamy infernal killcore3 kopenhagen laucin lostinspace luna mbt12"
_maps_capture5 = "mc-lab meltdown2 mercury metro monastery nevil_c new_energy nitro nmp4 nmp9 nucleus ogrosupply overdrive ow pandora paradigm pariah"
_maps_capture6 = "ph-capture pul1ctf reissen relic risk river_c river_keep ruby ruebli rust serenity skycastle-r snapper_rocks spcr stadium stronghold subterra"
_maps_capture7 = "suburb surge tempest tortuga triforts turbulence turmoil twinforts urban_c valhalla venice waltz xenon zamak"

_maps_ctf1 = "aastha abbey akimiski akroseum arbana asgard authentic autumn bad_moon berlin_wall bklyn breakout bt_falls campo capture_night casa catch22"
_maps_ctf2 = "collide core_refuge core_transfer croma ctf_suite daemex damnation desecration destiny disc disruption divine duomo dust2 earthsea earthstation enigma"
_maps_ctf3 = "eris eternal_valley europium evilness face-capture fire_keep flagstone forge forgotten fortress fragnostic fusion garden hallo harbor haste hidden"
_maps_ctf4 = "idris infamy kiryu kopenhagen l_ctf laucin luna mach2 mbt1 mbt4 mbt12 mc-lab meltdown2 mercury metro mill new_energy"
_maps_ctf5 = "nitro nucleus overdrive ow pandora pul1ctf ra recovery redemption regal reissen risk river_keep ruebli rust sacrifice shellshock2"
_maps_ctf6 = "shipwreck siberia snapper_rocks spcr stadium stronghold subterra suburb surge tatooine tectonic tejen tempest tortuga triforts tubes turbulence"
_maps_ctf7 = "twinforts unworld3 urban_c valhalla warlock wdcd xenon"

_maps_concept = "box_demo canyon door_demo firstevermap ladder platform_demo sauerbraten secondevermap spiralz"

_maps_sp = "mpsp6a mpsp6b mpsp6c mpsp9a mpsp9b mpsp9c mpsp10 lost level9 skrsp1 crnsp1"

_maps_rpg = "k_rpg1 rpg_01"

_maps_allffa     = (concat $_maps_ffa1 $_maps_ffa2 $_maps_ffa3 $_maps_ffa4 $_maps_ffa5 $_maps_ffa6 $_maps_ffa7 $_maps_ffa8 $_maps_ffa9 $_maps_ffa10 $_maps_ffa11)
_maps_allcapture = (concat $_maps_capture1 $_maps_capture2 $_maps_capture3 $_maps_capture4 $_maps_capture5 $_maps_capture6 $_maps_capture7)
_maps_allctf     = (concat $_maps_ctf1 $_maps_ctf2 $_maps_ctf3 $_maps_ctf4 $_maps_ctf5 $_maps_ctf6 $_maps_ctf7)
_maps_all        = (concat $_maps_allffa $_maps_allcapture $_maps_allctf $_maps_concept $_maps_sp $_maps_rpg)

_showmapshot = [
    guibar
    mname = (checkrolloveraction "map " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guilist [
        guiimage (concatword "packages/base/" $mname ".jpg") (checkrolloveraction "map ") 4 1 "data/cube.png" $mname
    ]
]

newgui "maps" [
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa1 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa2 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa3 ]
        _showmapshot
    ]
guitab "2"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa4 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa5 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa6 ]
        _showmapshot
    ]
guitab "3"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa7 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa8 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa9 ]
        _showmapshot
    ]
guitab "4"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa10 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ffa11 ]
        //guilist [ guistrut 15 1; _genmapitems $_maps_ffa12 ]
        guilist [ guistrut 15 1 ]
        _showmapshot
    ]
guitab "capture"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_capture1 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_capture2 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_capture3 ]
        _showmapshot
    ]
guitab "2"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_capture4 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_capture5 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_capture6 ]
        _showmapshot
    ]
guitab "3"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_capture7 ]
        guilist [ guistrut 15 1 ]
        guilist [ guistrut 15 1 ]
        _showmapshot
    ]
guitab "ctf"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf1 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf2 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf3 ]
        _showmapshot
    ]
guitab "2"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf4 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf5 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf6 ]
        _showmapshot
    ]
guitab "3"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf7 ]
        guilist [ guistrut 15 1 ]
        guilist [ guistrut 15 1 ]
        _showmapshot
    ]

guitab "concept"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_concept ]
        guilist [ guistrut 15 1 ]
        guilist [ guistrut 15 1 ]
        _showmapshot
    ]
]

newgui "capturemaps" [
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; _genmapitems $_maps_capture1 ]
      guilist [ guistrut 15 1; _genmapitems $_maps_capture2 ]
      guilist [ guistrut 15 1; _genmapitems $_maps_capture3 ]
      _showmapshot
    ]
guitab "2"
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; _genmapitems $_maps_capture4 ]
      guilist [ guistrut 15 1; _genmapitems $_maps_capture5 ]
      guilist [ guistrut 15 1; _genmapitems $_maps_capture6 ]
      _showmapshot
    ]
guitab "3"
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; _genmapitems $_maps_capture7 ]
      guilist [ guistrut 15 1 ]
      guilist [ guistrut 15 1 ]
      _showmapshot
    ]
] "capture maps"

newgui "ctfmaps" [
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf1 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf2 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf3 ]
        _showmapshot
    ]
guitab "2"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf4 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf5 ]
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf6 ]
        _showmapshot
    ]
guitab "3"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; _genmapitems $_maps_ctf7 ]
        guilist [ guistrut 15 1 ]
        guilist [ guistrut 15 1 ]
        _showmapshot
    ]
] "ctf maps"

_custommaps = ""
newgui "custommaps" [
    looplist curmap $_custommaps [
        guibutton $curmap (concat map $curmap) "cube"
    ]
] "custom maps"
showcustommaps = [
    _custommaps = ""
    loopfiles curmap "packages/base" "ogz" [
        if (< (indexof $_maps_all $curmap) 0) [
            _custommaps = (concat $_custommaps (escape $curmap))
        ]
    ]
    _custommaps = (sortlist $_custommaps x y [naturalsort $x $y])
    showgui "custommaps"
]

macro playermodelbutton [cleargui 1; playermodel %1]
newgui "playermodel" [
    guilist [
        guilist [
            loop i $playermodelnum [
                guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
            ]
        ]
        guibar
        _pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
        _pmidx = (max 0 (min (- $playermodelnum 1) $_pmidx))
        guilist [
            guilist [
                guispring
                guiplayerpreview $_pmidx 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $_pmidx)
                guispring
            ]
            guitextbox (playermodelstory $_pmidx) 40 9
        ]
    ]
]

_crosshairs = [
    "data/crosshair.png                   data/hit.png"
    "packages/crosshairs/x.png            packages/crosshairs/x-hit.png"
    "packages/crosshairs/x_dot.png        packages/crosshairs/x_dot-hit.png"
    "packages/crosshairs/o.png            packages/crosshairs/o-hit.png"
    "packages/crosshairs/o_x.png          packages/crosshairs/o_x-hit.png"
    "packages/crosshairs/o_dot.png        packages/crosshairs/o_dot-hit.png"
    "packages/crosshairs/dot.png          packages/crosshairs/dot-hit.png"
    "packages/crosshairs/dot_wide.png     packages/crosshairs/dot_wide-hit.png"
    "packages/crosshairs/star.png         packages/crosshairs/star-hit.png"
    "packages/crosshairs/wide.png         packages/crosshairs/wide-hit.png"
    "packages/crosshairs/circle_dot.png   packages/crosshairs/circle_dot_hit.png"
    "packages/crosshairs/cross_normal.png packages/crosshairs/cross_normal_hit.png"
    "packages/crosshairs/cross_whole.png  packages/crosshairs/cross_whole_hit.png"
    "packages/crosshairs/dot_normal.png   packages/crosshairs/dot_normal_hit.png"
    "packages/crosshairs/dot_whole.png    packages/crosshairs/dot_whole_hit.png"
    "packages/crosshairs/dot2.png         packages/crosshairs/dot2_hit.png"
]

newgui "crosshair" [
    guitext "crosshair size"
    guislider crosshairsize
    loop row (div (+ (listlen $_crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $_crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                    ] 0.5
                ]
            ]
        ]
    ]
]

newgui "master" [
  guistayopen [
    if (ismaster (getclientnum)) [
        guibutton "relinquish master" "setmaster 0"
    ] [
        guibutton "claim master" "setmaster 1"
    ]
    guilist [
        guitext "mastermode: "
        mm = (getmastermode)
        guiradio "open (0) " mm 0 "mastermode 0"
        guiradio "veto (1) " mm 1 "mastermode 1"
        guiradio "locked (2) " mm 2 "mastermode 2"
        guiradio "private (3) " mm 3 "mastermode 3"
    ]
    guibar
    looplist cn (listclients 0 0) [
        guilist [
            guitext (concatword (getclientname $cn) ": ") (getclienticon $cn)
            guibutton "kick " [kick @cn]
            guibutton "toggle spectator " [spectator @(= (isspectator $cn) 0) @cn]
            if (m_teammode (getmode)) [
                guibutton "switch team " [setteam @cn @(? (=s (getclientteam $cn) "good") "evil" "good")]
            ]
            if (ismaster $cn) [
                guibutton "take master" [setmaster 0 @cn]
            ] [
                guibutton "give master" [setmaster 1 @cn]
            ]
        ]
    ]
  ]
]

if (findfile "data/p1xbraten/master.cfg.gz") [
    exec "data/p1xbraten/master.cfg.gz"
]

_botmatchcount    =   5
_botmatchminskill =  50
_botmatchmaxskill = 100

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $_botmatchcount [addbot (rnd (+ $_botmatchmaxskill 1) $_botmatchminskill)]
    ]
]

newgui "botmatch" [
    guitext "bot amount"
    guislider _botmatchcount 0 32
    guitext "bot minimum skill"
    guislider _botmatchminskill 50 101 [if (< $_botmatchmaxskill $_botmatchminskill) [_botmatchmaxskill = $_botmatchminskill]]
    guitext "bot maximum skill"
    guislider _botmatchmaxskill 50 101 [if (> $_botmatchminskill $_botmatchmaxskill) [_botmatchminskill = $_botmatchmaxskill]]
    guibutton "start match.." [guionclear [startbotmatch]; showgui "gamemode"]
] "bot match"

newgui "campaign" [
    guibutton   "start Private Stan Sauer"  [showgui "privatestansauer"]
    guibutton   "start An Army Of One"      [showgui "armyofone"]
    guibutton   "start Lost"                [sp lost]                       "cube"
    guibutton   "start Meltdown"            [sp skrsp1]                     "cube"
    guibutton   "start Missile Pass"        [sp crnsp1]                     "cube"
    guibutton   "start Level 9"             [sp level9]                     "cube"
    guibar
    guibutton   "start DMSP map.."          [mode -2; showgui "maps"]
    guicheckbox "slow motion"               [slowmosp]
    guitext     "skill (default: 3)"
    guislider skill
]

newgui "armyofone" [
    guilist [
        guilist [
            guibutton "Part I"   [sp mpsp6a] "cube"
            guibutton "Part II"  [sp mpsp6b] "cube"
            guibutton "Part III" [sp mpsp6c] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"

newgui "privatestansauer" [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I"        [sp mpsp9a] "cube"
            guibutton "Run N' Gun Part II"       [sp mpsp9b] "cube"
            guibutton "Run N' Gun Part III"      [sp mpsp9c] "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" [sp mpsp10] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"

newgui "servers" [
    guistayopen [
        guiservers [
            guilist [
                guibutton     "update from master server" [updatefrommaster]
                guispring
                guistrut 2
                newfilterdesc = $filterservers
                guitext "search: " 0; guifield newfilterdesc 10 [filterservers $newfilterdesc]; guibutton "" [filterservers ""] "exit"
                guistrut 2
                guispring
                guicheckbox   "search LAN"                [searchlan]
                guistrut 2
                guicheckbox   "auto-update"               [autoupdateservers]
                guistrut 2
                guicheckbox   "auto-sort"                 [autosortservers]
                if (= $autosortservers 0) [
                    guistrut 2
                    guibutton "sort"                      [sortservers]
                ]
            ]
            guistrut 1
        ] 17
    ]
] "" [initservers]

newgui "gamemode" [
    guilist [
        guilist [
            guibutton "ffa"                [mode  0; showgui "maps"]
            guibutton "coop-edit"          [mode  1; showgui "maps"]
            guibutton "teamplay"           [mode  2; showgui "maps"]
            guibutton "instagib"           [mode  3; showgui "maps"]
            guibutton "instagib team"      [mode  4; showgui "maps"]
            guibutton "efficiency"         [mode  5; showgui "maps"]
            guibutton "efficiency team"    [mode  6; showgui "maps"]
            guibutton "tactics"            [mode  7; showgui "maps"]
            guibutton "tactics team"       [mode  8; showgui "maps"]
            guibutton "capture"            [mode  9; showgui "capturemaps"]
            guibutton "regen capture"      [mode 10; showgui "capturemaps"]
        ]
        guibar
        guilist [
            guibutton "ctf"                [mode 11; showgui "ctfmaps"]
            guibutton "insta ctf"          [mode 12; showgui "ctfmaps"]
            guibutton "efficiency ctf"     [mode 17; showgui "ctfmaps"]
            guibutton "protect"            [mode 13; showgui "ctfmaps"]
            guibutton "insta protect"      [mode 14; showgui "ctfmaps"]
            guibutton "efficiency protect" [mode 18; showgui "ctfmaps"]
            guibutton "hold"               [mode 15; showgui "capturemaps"]
            guibutton "insta hold"         [mode 16; showgui "capturemaps"]
            guibutton "efficiency hold"    [mode 19; showgui "capturemaps"]
            guibutton "collect"            [mode 20; showgui "ctfmaps"]
            guibutton "insta collect"      [mode 21; showgui "ctfmaps"]
            guibutton "efficiency collect" [mode 22; showgui "ctfmaps"]
        ]
    ]
]


showfileeditor = [
    guistayopen [
        guilist [
            guinoautotab [
                guieditor $arg1 $arg2 $arg3
                textinit $arg1 $arg1
            ]
            guistrut 1
            guilist [
                guispring
                guibutton "load"   [textfocus @arg1; textload @arg1]
                guispring
                guibutton "save"   [textfocus @arg1; textsave @arg1]
                guispring
                guibutton "exec"   [textfocus @arg1; textexec]
                guispring
                guibutton "copy"   [textfocus @arg1; textcopy]
                guispring
                guibutton "paste"  [textfocus @arg1; textpaste]
                guispring
                guibutton "select" [textfocus @arg1; textselectall]
                guispring
                guibutton "clear"  [textfocus @arg1; textclear]
                guispring
            ]
        ]
    ]
]

_notepadfile = "untitled.txt"

newgui "notepad" [
    guifield _notepadfile -30
    showfileeditor $_notepadfile -80 20
]

notepad = [
    if (> $numargs 0) [_notepadfile = $arg1]
    showgui "notepad"
]

newgui "pastebuffer" [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui "pastebuffer"]

newgui "scratchpad" [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec"   [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy"   [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste"  [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear"  [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui "scratchpad"]

newmapsize = 12
savemap_name = temp

newgui "editing" [ ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guikeeptab 1

    guibutton "toggle edit mode^t^t^f2[E]"  [edittoggle]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guitext "select^t^t^t^t^f2[mouse-left *drag]^t"
            guitext "extend selection^t^t^f2[mouse-right]"
            guitext "move selection^t^t^t^f2[mouse-right *drag]"
            guitext "deselect^t^t^t^t^f2[SPACE]"
            guitext "select all ents in box^t^f2[numpad-ENTER]"
        ]
        guilist [
            guitext "select corners^t^t^t^f2[mouse-middle *drag]"
            guitext "reorient^t^t^t^t^f2[mouse-right]"
            guitext "passthrough^t^t^t^f2[LCTRL *hold]"
            guitext "center view on ent^t^t^f2[COMMA + mousewheel]"
            guitext "select all ents matching^t^f2[N]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guitext "push/pull selection^t^t^f2[mousewheel]^t^t"
            guitext "change gridsize^t^t^f2[G + mousewheel]"
            guitext "mirror on selected axis^t^f2[X]"
        ]
        guilist [
            guitext "push cube face^t^t^f2[F + mousewheel]"
            guitext "push corner with cursor^t^f2[Q + mousewheel]"
            guitext "rotate^t^t^t^t^f2[R + mousewheel]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guitext "undo^t^t^t^t^t^f2[U] [Z]^t^t^t"
            guitext "copy^t^t^t^t^t^f2[C]"
            guitext "delete^t^t^t^t^f2[DELETE]"
        ]
        guilist [
            guitext "redo^t^t^t^t^t^f2[I]"
            guitext "paste^t^t^t^t^f2[V]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guitext     "change textures^t^t^f2[Y + mousewheel]^t"
            guicheckbox "texture all faces^t^t^f2[0]"       [allfaces]
            guicheckbox "toggle outline^t^t^t^f2[7]"        [outline]
        ]
        guilist [
            guibutton   "texture palette..^t^t^f2[F2]"      [showtexgui]
            guibutton   "materials.."                       [showgui "materials"]
            guicheckbox "toggle wireframe^t^t^f2[8]"        [wireframe]
        ]
    ]

guitab "ents" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [
        guilist [
            guibutton   "edit selected ent..^t^t^f2[F3]^t^t^t"          [showentgui]
            guicheckbox "entity snap-to-grid^t^t^f2[6]"                 [entselsnap]
        ]
        guilist [
            guibutton   "edit selected entity in console^t^f2[PERIOD]"  [selentedit]
            guicheckbox "entity selection"                              [entediting]
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guitext "drop entities"
    guilist [
        guilist [
            guiradio "at selection^t^t^t^t^t^t"             [entdrop 2]
            guiradio "at selection & to the floor"          [entdrop 3]
        ]
        guilist [
            guiradio "at current position"                  [entdrop 0]
            guiradio "at current position & to the floor"   [entdrop 1]
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guitext "newent *"
    guilist [
        guilist [
            guibutton "light..^t^t^t^t^t"           [resetlight; showgui "newlight"]
            guibutton "playerstart"                 [newent playerstart]
            guibutton "base"                        [newent base]
            guibutton "trigger.."                   [showgui "triggers"]
        ]
        guilist [
            guibutton "mapmodel..^t^t^f2[F4]^t^t"   [showgui mapmodels]
            guibutton "playerstart team-1"          [newent playerstart 1]
            guibutton "flag team-1"                 [newent flag 1]
            guibutton "monster.."                   [showgui "monsters"]
        ]
        guilist [
            guibutton "teleport.."                  [showgui "newteleport"]
            guibutton "playerstart team-2"          [newent playerstart 2]
            guibutton "flag team-2"                 [newent flag 2]
        ]
    ]
    guitext "pickup entities:"
    guilist [
        guilist [
            guibutton "shells^t^t^t^t^t^t^t"        [newent shells]
            guibutton "bullets"                     [newent bullets]
            guibutton "rockets"                     [newent rockets]
            guibutton "riflerounds"                 [newent riflerounds]
            guibutton "grenades"                    [newent grenades]
            guibutton "cartridges"                  [newent cartridges]
        ]
        guilist [
            guibutton "health"                      [newent health]
            guibutton "healthboost"                 [newent healthboost]
            guibutton "greenarmour"                 [newent greenarmour]
            guibutton "yellowarmour"                [newent yellowarmour]
            guibutton "quaddamage"                  [newent quaddamage]
            guibutton "select all pickup ents ^f2/findpickupents" [findpickupents]
        ]
    ]

guitab "lighting" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guibutton "maplighting..    ^f2/maplighting"    [showgui "maplighting"]
    guibar //-------------------------------------------------------------------------
    _lightingstuff

guitab "skybox" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [
        guistayopen [
            guilist [_genskyitems $_skies1]
            guilist [_genskyitems $_skies2]
            guilist [_genskyitems $_skies3]
            _skyboxpreview
        ]
    ]

guitab "sky" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    _skyboxcolour_r = (& (>> $skyboxcolour 16) 0xFF)
    _skyboxcolour_g = (& (>> $skyboxcolour  8) 0xFF)
    _skyboxcolour_b = (& (>> $skyboxcolour  0) 0xFF)
    guilist [
        guitext "skyboxcolour                                                              "
        guicolor $skyboxcolour
    ]
    guislider _skyboxcolour_r 0 255 [skyboxcolour $_skyboxcolour_r $_skyboxcolour_g $_skyboxcolour_b]
    guislider _skyboxcolour_g 0 255 [skyboxcolour $_skyboxcolour_r $_skyboxcolour_g $_skyboxcolour_b]
    guislider _skyboxcolour_b 0 255 [skyboxcolour $_skyboxcolour_r $_skyboxcolour_g $_skyboxcolour_b]
    guistrut 1 //-------------------------------------------------------------------------
    guilist [
        guitext "spinsky   "
        guibutton "-" [spinsky (+fround $spinsky -0.1)]
        guistrut 1
        guifield spinsky 5
        guistrut 1
        guibutton "+" [spinsky (+fround $spinsky 0.1)]
    ]
    guistrut 1 //-------------------------------------------------------------------------
    guitext "yawsky"
    guislider yawsky


guitab "cloudlayer" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "cloudlayer"
    guistayopen [
        guilist [
            guilist [
                guibutton "skyboxes/clouds01.png"           [cloudlayer "skyboxes/clouds01.png"]
                guibutton "skyboxes/clouds02.png"           [cloudlayer "skyboxes/clouds02.png"]
                guibutton "skyboxes/clouds03.png"           [cloudlayer "skyboxes/clouds03.png"]
                guibutton "skyboxes/white_bk.jpg"           [cloudlayer "skyboxes/white_bk.jpg"]
            ]
            guilist [
                guibutton "*no clouds*"                     [cloudlayer ""]
                guibutton "blindabuser/blindaclouds.png"    [cloudlayer "blindabuser/blindaclouds.png"]
                guibutton "skyboxes/cloud_ring.png"         [cloudlayer "skyboxes/cloud_ring.png"]
                guibutton "textures/razgriz/cloud03.png"    [cloudlayer "textures/razgriz/cloud03.png"]
                guibutton "textures/razgriz/cloud04.png"    [cloudlayer "textures/razgriz/cloud04.png"]
            ]
        ]
    ]
    guistrut 1 //-------------------------------------------------------------------------
    _cloudcolour_r = (& (>> $cloudcolour 16) 0xFF)
    _cloudcolour_g = (& (>> $cloudcolour  8) 0xFF)
    _cloudcolour_b = (& (>> $cloudcolour  0) 0xFF)
    guilist [
        guitext "cloudcolour                                                              "
        guicolor $cloudcolour
    ]
    guislider _cloudcolour_r 0 255 [cloudcolour $_cloudcolour_r $_cloudcolour_g $_cloudcolour_b]
    guislider _cloudcolour_g 0 255 [cloudcolour $_cloudcolour_r $_cloudcolour_g $_cloudcolour_b]
    guislider _cloudcolour_b 0 255 [cloudcolour $_cloudcolour_r $_cloudcolour_g $_cloudcolour_b]
    guistrut 1 //-------------------------------------------------------------------------
    guilist [
        guilist [
            guilist [
                guitext "cloudalpha^t"
                guibutton "-" [cloudalpha (+fround $cloudalpha -0.05)]
                guistrut 1
                guifield cloudalpha 5
                guistrut 1
                guibutton "+" [cloudalpha (+fround $cloudalpha 0.05)]
            ]

            guilist [
                guitext "cloudfade^t^t"
                guibutton "-" [cloudfade (+fround $cloudfade -0.05)]
                guistrut 1
                guifield cloudfade 5
                guistrut 1
                guibutton "+" [cloudfade (+fround $cloudfade 0.05)]
            ]
        ]
        guistrut 5
        guilist [
            guilist [
                guitext "cloudheight^t"
                guibutton "-" [cloudheight (+fround $cloudheight -0.01)]
                guistrut 1
                guifield cloudheight 5
                guistrut 1
                guibutton "+" [cloudheight (+fround $cloudheight 0.01)]
            ]

            guilist [
                guitext "cloudscale^t^t"
                guibutton "-" [cloudscale (+fround $cloudscale -0.0625)]
                guistrut 1
                guifield cloudscale 5
                guistrut 1
                guibutton "+" [cloudscale (+fround $cloudscale 0.0625)]
            ]
        ]
    ]
    guistrut 1 //-------------------------------------------------------------------------
    guilist [
        guilist [
            guilist [
                guitext "cloudoffsetx^t"
                guibutton "-" [cloudoffsetx (+fround $cloudoffsetx -0.01)]
                guistrut 1
                guifield cloudoffsetx 5
                guistrut 1
                guibutton "+" [cloudoffsetx (+fround $cloudoffsetx 0.01)]
            ]
            guilist [
                guitext "cloudoffsety^t"
                guibutton "-" [cloudoffsety (+fround $cloudoffsety -0.01)]
                guistrut 1
                guifield cloudoffsety 5
                guistrut 1
                guibutton "+" [cloudoffsety (+fround $cloudoffsety 0.01)]
            ]
        ]
        guistrut 5
        guilist [
            guilist [
                guitext "cloudscrollx^t"
                guibutton "-" [cloudscrollx (+fround $cloudscrollx -0.01)]
                guistrut 1
                guifield cloudscrollx 5
                guistrut 1
                guibutton "+" [cloudscrollx (+fround $cloudscrollx 0.01)]
            ]
            guilist [
                guitext "cloudscrolly^t"
                guibutton "-" [cloudscrolly (+fround $cloudscrolly -0.01)]
                guistrut 1
                guifield cloudscrolly 5
                guistrut 1
                guibutton "+" [cloudscrolly (+fround $cloudscrolly 0.01)]
            ]
            guilist [
                guitext "spincloudlayer^t"
                guibutton "-" [spincloudlayer (+fround $spincloudlayer -0.1)]
                guistrut 1
                guifield spincloudlayer 5
                guistrut 1
                guibutton "+" [spincloudlayer (+fround $spincloudlayer 0.1)]
            ]
        ]
    ]

guitab "fog" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "fog"
    guislider fog 128 8192
    _fogcolour_r = (& (>> $fogcolour 16) 0xFF)
    _fogcolour_g = (& (>> $fogcolour  8) 0xFF)
    _fogcolour_b = (& (>> $fogcolour  0) 0xFF)
    guistrut 1
    guilist [
        guitext "fogcolour                                                              "
        guicolor $fogcolour
    ]
    guislider _fogcolour_r 0 255 [fogcolour $_fogcolour_r $_fogcolour_g $_fogcolour_b]
    guislider _fogcolour_g 0 255 [fogcolour $_fogcolour_r $_fogcolour_g $_fogcolour_b]
    guislider _fogcolour_b 0 255 [fogcolour $_fogcolour_r $_fogcolour_g $_fogcolour_b]
    guistrut 1 //-------------------------------------------------------------------------
    _fogdomecolour_r = (& (>> $fogdomecolour 16) 0xFF)
    _fogdomecolour_g = (& (>> $fogdomecolour  8) 0xFF)
    _fogdomecolour_b = (& (>> $fogdomecolour  0) 0xFF)
    guilist [
        guitext "fogdomecolour  (0 = use fogcolour)                                        "
        guicolor $fogdomecolour
    ]
    guislider _fogdomecolour_r 0 255 [fogdomecolour $_fogdomecolour_r $_fogdomecolour_g $_fogdomecolour_b]
    guislider _fogdomecolour_g 0 255 [fogdomecolour $_fogdomecolour_r $_fogdomecolour_g $_fogdomecolour_b]
    guislider _fogdomecolour_b 0 255 [fogdomecolour $_fogdomecolour_r $_fogdomecolour_g $_fogdomecolour_b]
    guistrut 1 //-------------------------------------------------------------------------
    guilist [
        guilist [
            guilist [
                guitext "fogdomemax^t"
                guibutton "-" [fogdomemax (+fround $fogdomemax -0.01)]
                guistrut 1
                guifield fogdomemax 5
                guistrut 1
                guibutton "+" [fogdomemax (+fround $fogdomemax 0.01)]
            ]
            guilist [
                guitext "fogdomemin^t"
                guibutton "-" [fogdomemin (+fround $fogdomemin -0.01)]
                guistrut 1
                guifield fogdomemin 5
                guistrut 1
                guibutton "+" [fogdomemin (+fround $fogdomemin 0.01)]
            ]
        ]
        guistrut 4
        guilist [
            guilist [
                guitext "fogdomeclip^t"
                guibutton "-" [fogdomeclip (+fround $fogdomeclip -0.01)]
                guistrut 1
                guifield fogdomeclip 5
                guistrut 1
                guibutton "+" [fogdomeclip (+fround $fogdomeclip 0.01)]
            ]

            guilist [
                guitext "fogdomeheight^t"
                guibutton "-" [fogdomeheight (+fround $fogdomeheight -0.01)]
                guistrut 1
                guifield fogdomeheight 5
                guistrut 1
                guibutton "+" [fogdomeheight (+fround $fogdomeheight 0.01)]
            ]
        ]
        guistrut 4
        guilist [
            guicheckbox "fogdomecap^t^t^t^t^t^t" [fogdomecap]
            guicheckbox "fogdomeclouds"          [fogdomeclouds]
        ]
    ]

guitab "height&blendmap" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [
        guilist [
            guitext "toggle heightmap mode^t^t^f2[H]^t^t^t"
            guitext "push/pull heightmap^t^t^t^f2[mousewheel]"
        ]
        guilist [
            guitext "select heightmap brush^t^f2[B + mousewheel]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guitext "blend paint mode"
    guilist [
        guilist [
            guiradio "off^t^t^t^t^t^t^f2[numpad 0]^t"    [blendpaintmode 0]
            guiradio "dig^t^t^t^t^t^t^f2[numpad 2]"      [blendpaintmode 2]
            guiradio "inverted dig^t^t^t^t^f2[numpad 4]" [blendpaintmode 4]
        ]
        guilist [
            guiradio "replace^t^t^t^t^f2[numpad 1]"      [blendpaintmode 1]
            guiradio "fill^t^t^t^t^t^f2[numpad 3]"       [blendpaintmode 3]
            guiradio "inverted fill^t^t^t^f2[numpad 5]"  [blendpaintmode 5]
        ]
    ]
    guitext "paint blendmap^t^t^t^f2[mouse-left]"
    guibutton "change blend brush^t^t^t^f2[mousewheel]"           [nextblendbrush]
    guibutton "rotate blend brush^t^t^t^f2[mouse-right]"          [rotateblendbrush]
    guibutton "clear entire blendmap^t^t^f2/clearblendmap"        [clearblendmap]
    guibutton "clear blendmap in selection^t^f2/clearblendmapsel" [clearblendmapsel]

guitab "map" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [
        guitext   "save map:"
        guistrut 1
        guifield  savemap_name 20 [ savemap $savemap_name ]
    ]
    guibutton [load map '@savemap_name']           [map $savemap_name]
    guistrut 1
    guibutton "newmap (default size: 12)"          [newmap $newmapsize]
    guislider "newmapsize" 10 16
    guistrut 1
    guibutton "increase mapsize (2x)"              [mapenlarge]
    guistrut 1
    guitext   "max undo size (default: 8 MB)"
    guislider "undomegs" 1 20
    guibutton "optimize geometry"                  [remip]
    guibutton "set map title/author"               [saycommand [/maptitle @maptitle]]
    guistrut 1
    guibutton "show custom maps"                   [showcustommaps]

guitab "cfg" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext (mapcfgname)
    guistrut .5
    showfileeditor (mapcfgname) -80 17
]

_skies1 = [
    aftas/sky/orbe
    blindabuser/blindasky
    blindabuser/blindasky2
    darc/forge
    dash/moon
    hazel/green
    humus/meadow
    ik2k/env/iklake
    jon/prarie
    mayhem/duomo
    mayhem/eclipse
    meister/uzze
    penguins/arid
    penguins/arid2
    penguins/cocoa
    penguins/divine
    penguins/harmony
    penguins/mellow
    penguins/mystic
]
_skies2 = [
    penguins/violence
    penguins/wrath
    penguins/yonder
    penguins/zeus
    ratboy/skyboxes/coward
    shmutzwurst/london
    skyboxes/black
    skyboxes/bbk/d
    skyboxes/bbk/dark2
    skyboxes/bbk/n1
    skyboxes/bbk/t
    skyboxes/clearsky052
    skyboxes/darkness
    skyboxes/evilsky
    skyboxes/morning
    skyboxes/remus/sky01
    skyboxes/turbulent
    skyboxes/ulukai/corona
    skyboxes/white
]
_skies3 = [
    socksky/barren
    socksky/desert
    socksky/devpun
    socksky/emerald
    socksky/frozen
    socksky/frozendusk
    socksky/grave
    socksky/mars
    socksky/nightball
    socksky/stratosphere
    socksky/valley
    staffy/staffy
]

_genskyitems = [
    looplist cursky $arg1 [
        guibutton $cursky (concat skybox $cursky)
    ]
]

_skyboxpreview = [
    guibar
    _skyshotname = (checkrolloveraction "skybox " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guiimage (concatword "<thumbnail:128>packages/" $_skyshotname "_ft") (checkrolloveraction "skybox ") 4 1 "data/cube.png" (chopstr $_skyshotname -16 "..")
]

_lightingstuff = [
    guitext "lightprecision"
    guilistslider lightprecision "8 16 32 48 64 96 128 256"
    guitext "lightlod (>= use lightprecision, < use lightprecision/2)"
    guislider lightlod
    guitext "lighterror"
    guislider lighterror
    guitext "lightthreads"
    guislider lightthreads
    guilist [
        guitext "blurskylight^t"
        guiradio "0 " blurskylight 0
        guiradio "1 " blurskylight 1
        guiradio "2 " blurskylight 2
    ]
    guilist [
        guitext "blurlightmaps^t"
        guiradio "0 " blurlms 0
        guiradio "1 " blurlms 1
        guiradio "2 " blurlms 2
    ]
    guibar //-------------------------------------------------------------------------
    guibutton "calclight 1  (slow: 8xAA)" "calclight 1"
    guibutton "calclight -1 (quick: no AA, no model shadows)"   "calclight -1"
    guicheckbox "fullbright  ^f2[L]" fullbright
]

maplighting = [showgui "maplighting"]
newgui "maplighting" [ ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [
        guilist [ // Prevent guiautotab from breaking menu.
            _sunlight_r = (& (>> $sunlight 16) 0xFF)
            _sunlight_g = (& (>> $sunlight  8) 0xFF)
            _sunlight_b = (& (>> $sunlight  0) 0xFF)
            guilist [
                guitext "sunlight^t^t^t^t^t^t^t^t"
                guicolor $sunlight
            ]
            guislider _sunlight_r 0 255 [sunlight $_sunlight_r $_sunlight_g $_sunlight_b]
            guislider _sunlight_g 0 255 [sunlight $_sunlight_r $_sunlight_g $_sunlight_b]
            guislider _sunlight_b 0 255 [sunlight $_sunlight_r $_sunlight_g $_sunlight_b]

            guilist [
                guitext "sunlightscale "
                guifield sunlightscale 8
            ]
            guitext "sunlightpitch "; guislider sunlightpitch
            guitext "sunlightyaw "  ; guislider sunlightyaw
            guibutton "Get sun pitch & yaw from camera.  ^f2/getsundir" [getsundir]

            guibar //-------------------------------------------------------------------------

            _skylight_r = (& (>> $skylight 16) 0xFF)
            _skylight_g = (& (>> $skylight  8) 0xFF)
            _skylight_b = (& (>> $skylight  0) 0xFF)
            guilist [
                guitext "skylight^t^t^t^t^t^t^t^t"
                guicolor $skylight
            ]
            guislider _skylight_r 0 255 [skylight $_skylight_r $_skylight_g $_skylight_b]
            guislider _skylight_g 0 255 [skylight $_skylight_r $_skylight_g $_skylight_b]
            guislider _skylight_b 0 255 [skylight $_skylight_r $_skylight_g $_skylight_b]

            guibar //-------------------------------------------------------------------------

            _ambient_r = (& (>> $ambient 16) 0xFF)
            _ambient_g = (& (>> $ambient  8) 0xFF)
            _ambient_b = (& (>> $ambient  0) 0xFF)
            guilist [
                guitext "ambient^t^t^t^t^t^t^t^t"
                guicolor $ambient
            ]
            guislider _ambient_r 0 255 [ambient $_ambient_r $_ambient_g $_ambient_b]
            guislider _ambient_g 0 255 [ambient $_ambient_r $_ambient_g $_ambient_b]
            guislider _ambient_b 0 255 [ambient $_ambient_r $_ambient_g $_ambient_b]
        ]
    ]
guitab "settings" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    _lightingstuff

]

newgui "materials" [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high (3)"  [waterlod 3]
    //guibutton "waterlod high      (2)"  [waterlod 2]
    //guibutton "waterlod default   (1)"  [waterlod 1]
    //guibutton "waterlod low       (0)"  [waterlod 0]

    //guibutton "watersubdiv very fine (0)"  [watersubdiv 0]
    //guibutton "watersubdiv fine      (1)"  [watersubdiv 1]
    //guibutton "watersubdiv default   (2)"  [watersubdiv 2]
    //guibutton "watersubdiv coarse    (3)"  [watersubdiv 3]

    //guibutton "water waves on"  [waterwaves 1]
    //guibutton "water waves off" [waterwaves 0]
]

newgui "triggers" [
    guibutton "invisible"   [BTmm = -1; showgui "trigger2"]
    guibutton "a carrot"    [BTmm = 23; showgui "trigger2"]
    guibutton "switch #1"   [BTmm = 24; showgui "trigger2"]
    guibutton "switch #2"   [BTmm = 25; showgui "trigger2"]
    guibutton "door #1"     [BTmm = 26; showgui "trigger2"]
    guibutton "door #2"     [BTmm = 27; showgui "trigger2"]
    guibutton "door #3"     [BTmm = 28; showgui "trigger2"]
]

newgui "trigger2" [
    guibutton "animation loop"                      [BTtt =  0; showgui "trigger3"]
    guibutton "trigger once (popback)"              [BTtt =  1; showgui "trigger3"]
    guibutton "trigger once (popback & rumble)"     [BTtt =  2; showgui "trigger3"]
    guibutton "trigger once"                        [BTtt =  3; showgui "trigger3"]
    guibutton "trigger once (rumble)"               [BTtt =  4; showgui "trigger3"]
    guibutton "trigger multiple (popback)"          [BTtt =  5; showgui "trigger3"]
    guibutton "trigger multiple (popback & rumble)" [BTtt =  6; showgui "trigger3"]
    guibutton "trigger multiple"                    [BTtt =  7; showgui "trigger3"]
    guibutton "trigger multiple (rumble)"           [BTtt =  8; showgui "trigger3"]
    guibutton "door (open once)"                    [BTtt =  9; showgui "trigger3"]
    guibutton "door (auto close)"                   [BTtt = 10; showgui "trigger3"]
    guibutton "door (locked)"                       [BTtt = 11; showgui "trigger3"]
    guibutton "vanishing trigger"                   [BTtt = 12; showgui "trigger3"]
    guibutton "vanishing trigger (rumble)"          [BTtt = 13; showgui "trigger3"]
    guibutton "End Of Level Trigger"                [BTtt = 29; showgui "trigger3"]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui "trigger3" [
    loop i 9 [
        _tjt = (concat tag (concatword # $i))
        _tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $_tjt $_tjc
    ]
]

resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [
    _lightr = $arg1
    _lightg = $arg2
    _lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        _lightr = (div $_lightr 2); _lightg = (div $_lightg 2); _lightb = (div $_lightb 2)
    ] [
        _lightr = (min 255 (* $_lightr 2)); _lightg = (min 255 (* $_lightg 2)); _lightb = (min 255 (* $_lightb 2))
    ]
]

lightcmd = [
    result (concat newent light $lightradius $_lightr $_lightg $_lightb)
]

newgui "newlight" [
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $_lightr 256) $_lightg) 256) $_lightb)
    guislider _lightr 0 255
    guislider _lightg 0 255
    guislider _lightb 0 255
    guilist [
        guicheckbox "bright"    lightbright 1 0 [lightscale]
        guibar
        guiradio "white"        lightcolour 0 [lightset 255 255 255]
        guiradio "blue"         lightcolour 1 [lightset 192 192 255]
        guiradio "red"          lightcolour 2 [lightset 255 192 192]
        guiradio "green"        lightcolour 3 [lightset 192 255 192]
        guiradio "yellow"       lightcolour 4 [lightset 255 255 192]
        guiradio "purple"       lightcolour 5 [lightset 255 192 255]
        guiradio "turquoise"    lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui "newteleport" [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

getmapmodels = [
    loopconcat i $nummapmodels [
        if $arg2 [ if (mapmodelloaded $i) [ $arg1 $i ] ] [ $arg1 $i ]
    ]
]

mmguiwidth = 7
mmguiheight = 4

newgui "mapmodels" [
    guikeeptab 1

    local mmarea mmid

    mmguiarea = (* $mmguiwidth $mmguiheight)

    guinoautotab [
        loop n (div (+ $nummapmodels (- $mmguiarea 1)) $mmguiarea) [
            if $n [guitab (+ $n 1)]
            if (= $guitabnum (+ $n 1)) [
                loop y $mmguiheight [
                    guilist [
                        loop x $mmguiwidth [
                            mmid = (+ (* $n $mmguiarea) (+ (* $y $mmguiwidth) $x))
                            guimodelpreview (mapmodelname $mmid) "mapmodel" [newent mapmodel @mmid] 3 1 $mmid 1
                        ]
                    ]
                ]
                guistrut 0.2
                guititle (concatword "^f7" (strreplace (strreplace $guirolloverpreviewname "\" "/") "/" "^fc/^f7"))
            ]
        ]
    ]
]

newgui "monsters" [
    guibutton "ogro / fireball"         [newent monster 0]
    guibutton "rhino / chaingun"        [newent monster 1]
    guibutton "ratamahatta / shotgun"   [newent monster 2]
    guibutton "slith / rifle"           [newent monster 3]
    guibutton "bauul / RL"              [newent monster 4]
    guibutton "hellpig / bite"          [newent monster 5]
    guibutton "knight / iceball"        [newent monster 6]
    guibutton "goblin / slimeball"      [newent monster 7]
    guibutton "spider / grenade"        [newent monster 8]
]

newgui "postfx" [
    guibutton "(effect OFF)"            [clearpostfx]
    guibutton "bloom (subtle: 30%)"     [bloom 0.3]
    guibutton "bloom (bright: 55%)"     [bloom 0.55]
    guibutton "bloom (intense: 80%)"    [bloom 0.8]
    guibutton "rotoscope"               [rotoscope 1]
    guibutton "rotoscope + blur3"       [rotoscope 1 1]
    guibutton "rotoscope + blur5"       [rotoscope 1 2]
    guibutton "sobel"                   [setpostfx sobel]
    guibutton "invert"                  [setpostfx invert]
    guibutton "gbr"                     [setpostfx gbr]
    guibutton "bw"                      [setpostfx bw]
    guibutton "blur3"                   [setpostfx hblur3; addpostfx vblur3]
    guibutton "blur5"                   [setpostfx hblur5; addpostfx vblur5]
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"])
]

newgui options [ /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guilist [ // horizontal
        guilist [ // vertical
            guitext "general" 0
            guistrut 4
            guitext "models" 0
            guistrut 6
            guitext "minimap" 0
            guistrut 2
            guitext "reset" 0
        ]
        guistrut 2
        guilist [
            guilist [
                guicheckbox "2D menus"     [gui2d]
                guistrut 1
                guicheckbox "click tab"    [guiclicktab]
                guistrut 1
                guicheckbox "menu fade in" [guifadein]
            ]
            guilist [
                guicheckbox "hit sound"              [hitsound]
                guistrut 1
                guicheckbox "blood"                  [blood]
                guistrut 1
                guicheckbox "outline capture meters" [outlinemeters]
            ]
            guicheckbox "show scoreboard at death"               [deathscore]
            guistayopen [ guibutton "adjust weapon trail colors" [showgui trailcolors] ]

            guistrut 1

            guilist [
                guicheckbox "ragdoll deaths" [ragdoll]
                if $ragdoll [
                    guibar
                    guicheckbox "keep after respawn" [ragdollmillis] 10000
                ]
            ]
            if $ragdoll [
                guitext "ragdoll velocity multiplier" 0
                guislider deadpush
            ]
            guicheckbox "hide dead players" [hidedead]
            guilist [
                guicheckbox "fullbright player models" [fullbrightmodels] 60 0
                if $fullbrightmodels [
                    guibar
                    guiradio "subtle"     fullbrightmodels  60
                    guistrut 1
                    guiradio "bright"     fullbrightmodels 100
                    guistrut 1
                    guiradio "overbright" fullbrightmodels 150
                    guistrut 1
                    guiradio "max"        fullbrightmodels 200
                ]
            ]
            guilist [
                guicheckbox "force matching player models" [forceplayermodels]
                guistrut 1
                guicheckbox "always use team skins"        [teamskins]
            ]

            if (! $ragdoll) [
                guistrut 3
            ] [
                guistrut 1
            ]

            guicheckbox "numbered capture bases" [basenumbers]
            guicheckbox "show teammates"         [radarteammates] 1

            guistrut 1
            guibutton "restore defaults (clears ALL settings)" [exec restore.cfg]
        ]
    ]

    guitab "hud" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guistayopen [
        guilist [
            guilist [
                guicheckbox "show FPS"          [showfps]
                guicheckbox "show wall clock"   [wallclock]
                guistrut 1
                guicheckbox "damage screen"     [damagescreen]
                guicheckbox "damage compass"    [damagecompass]
                guistrut 1
                guicheckbox "hudguns"           [hudgun]
                guicheckbox "crosshair effects" [crosshairfx]
                guistrut 1
                guicheckbox "ammobar"           [ammobar]
                guicheckbox "hudscore"          [hudscore]
                guicheckbox "gameclock"         [gameclock]
                guicheckbox "frag messages"     [hudfragmessages]
                guitext "scoreboard" 0
            ]

            guistrut 2
            guibar
            guistrut 2

            guilist [
                guistrut 1 // fps
                if $wallclock [
                    guilist [
                        guicheckbox "24 hour"   [wallclock24]
                        guistrut 1
                        guicheckbox "seconds"   [wallclocksecs]
                    ]
                ] [
                    guistrut 1
                ]
                guistrut 1
                guistrut 2 // damage
                guistrut 1
                if $hudgun [
                    guilist [
                        guicheckbox "sway"        [hudgunsway]
                        guistrut 1
                        guicheckbox "muzzle flash" [muzzleflash]
                        guistrut 1
                        guicheckbox "muzzle light" [muzzlelight]
                    ]
                ] [
                    guistrut 1
                ]
                if $crosshairfx [
                    guilist [
                        guicheckbox "health colors" [crosshaircolors]
                        guistrut 1
                        guicheckbox "teammates"     [teamcrosshair]
                        guistrut 1
                        guicheckbox "hits"          [hitcrosshair] 425
                        guistrut 1
                        guicheckbox "reload"        [crosshairreloadfade]
                    ]
                ] [
                    guistrut 1
                ]

                guistrut 1

                if $ammobar [
                    guibutton "adjust ammobar"          [showgui "ammobar"]
                ] [
                    guistrut 1
                ]
                if $hudscore [
                    guibutton "adjust hudscore"         [showgui "hudscore"]
                ] [
                    guistrut 1
                ]
                if $gameclock [
                    guibutton "adjust gameclock"        [showgui "gameclock"]
                ] [
                    guistrut 1
                ]
                if $hudfragmessages [
                    guibutton "adjust frag messages"    [showgui "hudfragmessages"]
                ] [
                    guistrut 1
                ]
                guibutton "adjust scoreboard"           [showgui "scoreboard"]
            ]
        ]
    ]

    guitab "gfx" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guistrut 1

    guistayopen [
        guilist [
            guilist [
                guitext "shaders" " "

                guitext "water" " "

                guitext "waterfalls" " "

                guicheckbox "^f3soft shadows"   [shadowmap]

                guicheckbox "^f3glare"          [glare]

                guicheckbox "^f3motion blur"    [motionblur]

                guicheckbox "^f3grass"          [grass]


                guicheckbox "^f0dynamic lights" [maxdynlights] 3 0
                guicheckbox "^f0soft particles" [depthfx]


                guicheckbox "^f0glass reflection" [glassenv]
                guicheckbox "^f0decals"         [decals]
                guicheckbox "^f0fix t-joints"   [filltjoints]
                guitext "textures" " "
                guitext "models" " "
            ]

            guistrut 2
            guibar
            guistrut 2

            guilist [
                guilist  [
                    guiradio "^f0low detail"     shaderdetail 1
                    guistrut 1
                    guiradio "^f2high detail"    shaderdetail 3
                ]
                guilist [
                    guicheckbox "^f2refraction" [waterrefract]
                    guistrut 1
                    guicheckbox "^f3reflection" [waterreflect]
                    guistrut 1
                    guicheckbox "^f0caustics"   [caustics]
                    guistrut 1
                    guicheckbox "^f0animation"  [vertwater]
                ]
                guilist [
                    guicheckbox "^f2refraction" [waterfallrefract]
                    guistrut 1
                    guicheckbox "^f0reflection" [waterfallenv]
                ]
                guilist [
                    if $shadowmap [
                        guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1]
                        guistrut 1
                        guiradio "^f3high quality"   shadowmapsize 10 [blurshadowmap 2]
                    ] [
                        guicheckbox "^f0blob shadows" [blobs]
                    ]
                ]
                if $glare [
                    glarepreset = 0
                    if (= $glarescale 1) [
                        if (= $blurglare 4) [glarepreset = 1]
                        if (= $blurglare 7) [glarepreset = 3]
                    ]
                    if (= $glarescale 2) [
                        if (= $blurglare 3) [glarepreset = 2]
                        if (= $blurglare 7) [glarepreset = 4]
                    ]
                    guilist [
                        guiradio "^f2subtle"  glarepreset 1 [blurglare 4; glarescale 1]
                        guistrut 1
                        guiradio "^f2glowy"   glarepreset 2 [blurglare 3; glarescale 2]
                        guistrut 1
                        guiradio "^f3soft"    glarepreset 3 [blurglare 7; glarescale 1]
                        guistrut 1
                        guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2]
                    ]
                ] [
                    guistrut 1
                ]
                if $motionblur [
                    guilist [
                        guiradio "^f3subtle"   motionblurscale 0.5
                        guistrut 1
                        guiradio "^f3moderate" motionblurscale 0.65
                        guistrut 1
                        guiradio "^f3intense"  motionblurscale 0.8
                    ]
                ] [
                    guistrut 1
                ]
                if $grass [
                    guilist [
                        guiradio "^f2quick fade"    grassdist 128
                        guistrut 1
                        guiradio "^f2moderate fade" grassdist 256
                        guistrut 1
                        guiradio "^f3slow fade"     grassdist 512
                    ]
                ] [
                    guistrut 1
                ]
                if $maxdynlights [
                    guilist [
                        guiradio "^f0medium quality" maxdynlights 3
                        guistrut 1
                        guiradio "^f2high quality"   maxdynlights 5
                    ]
                ] [
                    guistrut 1
                ]
                if $depthfx [
                    guilist [
                        guiradio "^f0low quality"    depthfxsize 7  [blurdepthfx 1]
                        guistrut 1
                        guiradio "^f2medium quality" depthfxsize 10 [blurdepthfx 0]
                        guistrut 1
                        guiradio "^f3high quality"   depthfxsize 12 [blurdepthfx 0]
                    ]
                ] [
                    guistrut 1
                ]
                guistrut 1
                if $decals [
                    guilist [
                        guiradio "^f0quick fade" decalfade 10000 [maxdecaltris 1024]
                        guistrut 1
                        guiradio "^f2slow fade"  decalfade 60000 [maxdecaltris 4096]
                    ]
                ][
                    guistrut 1
                ]
                guistrut 1
                guilist [
                    guiradio "^f0low quality"    maxtexsize 256
                    guistrut 1
                    guiradio "^f0medium quality" maxtexsize 512
                    guistrut 1
                    guiradio "^f2high quality"   maxtexsize 0
                ]
                guilist [
                    guicheckbox "^f0reflection" [envmapmodels]
                    guistrut 1
                    guicheckbox "^f2bumpmap"    [bumpmodels]
                ]

            ]
        ]
    ]

    guitab "display" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "field of view (default: 100)" 0
    guislider fov
    guistrut 1
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guistrut 1
    guitext "gamma (default: 100)" 0
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)" 0
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "z-buffer depth (default: 0)" 0
    guilistslider depthbits "0 16 24 32"
    guitext "anisotropic filtering (default: 0)" 0
    guilistslider aniso "0 2 4 8 16"
    guilist [
        guicheckbox "bilinear filtering" bilinear
        guistrut 1
        guicheckbox "trilinear filtering (mipmaps)" trilinear
    ]

    guitab "res" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guistayopen [
      guilist [
        guilist [
            guitext "4:3" 0
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guispring
        guilist [
            guitext "16:10" 0
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 2880 1800)
            @@@@(resbutton 3200 2000)
            @@@@(resbutton 3840 2400)
        ]
        guispring
        guilist [
            guitext "16:9" 0
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 2560 1440)
            @@@@(resbutton 2880 1620)
            @@@@(resbutton 3200 1800)
            @@@@(resbutton 3840 2160)
        ]
        guispring
        guilist [
            guitext "5:4" 0
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guispring
        guilist [
            guitext "5:3" 0
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guistrut 1
            guitext "custom" 0
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab "sound" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "sound volume" 0
    guislider soundvol
    guitext "music volume" 0
    guislider musicvol
    guistrut 1
    guitext "sound channels" 0
    guislider soundchans
    guitext "sound frequency" 0
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guistrut 1
    guicheckbox "Mumble positional audio" mumble

    guitab "mouse" ////////////////////////////////////////////////////////////////////////////////////////////////////////////

    guilist [
        guilist [
            guitext "mouse:" 0
            guitext "^f7sensitivity" 0
            guitext "^f7UI sensitivity" 0
            guitext "^f7invert" 0
            guistrut 1
            guitext "crosshair:" 0
            guitext "^f7size" 0
            guitext "^f7change" 0
        ]
        guistrut 2
        guilist [
            guistrut 1

            guilist [
                guistayopen [ guibutton "-" [sensitivity (+fround $sensitivity -0.01)] ]
                guistrut 1
                guifield sensitivity 4
                guistrut 1
                guistayopen [ guibutton "+" [sensitivity (+fround $sensitivity 0.01)] ]
            ]
            guilist [
                guistayopen [ guibutton "-" [guisens (+fround $guisens -0.01)] ]
                guistrut 1
                guifield guisens 4
                guistrut 1
                guistayopen [ guibutton "+" [guisens (+fround $guisens 0.01)] ]
            ]
            guicheckbox "" invmouse

            guistrut 1

            guiimage (getcrosshair) [showgui crosshair] 0.5
            guilist [
                guistayopen [ guibutton "-" [crosshairsize (+ $crosshairsize -1)] ]
                guistrut 1
                guifield crosshairsize 3
                guistrut 1
                guistayopen [ guibutton "+" [crosshairsize (+ $crosshairsize 1)] ]
            ]
        ]
    ]

    guitab "keys" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guitext "(basic keybinds, for anything more use the 'bind' command)"
    guitext "select action to bind and press desired keys (ESC when done)"

    guistrut 1

    bindidx = 0
    guilistsplit n 2 $bindactions [
        guilist [
            guitext (tabify (concatword $n ": ") 4) 0
            [newbinds@bindidx] = (searchbinds $n)
            guikeyfield [newbinds@bindidx] -12 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@bindidx] [bind $j [@@@n]]
            ]
            bindidx = (+ $bindidx 1)
        ]
    ] [
        guispring
    ]
    guispring

    guitab "console" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    guistayopen [
      guilist [
        guilist [
            guitext "top left console filter:" 0
            guibitfield "important information" confilter 0x11
            guibitfield "warnings" confilter 0x02
            guibitfield "errors" confilter 0x04
            guibitfield "script messages" confilter 0x20
            guibitfield "chat" confilter 0x100
            guibitfield "team chat" confilter 0x200
            guibitfield "game events" confilter 0x400
            guibitfield "self frags" confilter 0x800
            guibitfield "other player frags" confilter 0x1000
            guibitfield "team kills" confilter 0x2000
        ]
        guispring
        guilist [
            guitext "full console filter:" 0
            guibitfield "important information" fullconfilter 0x11
            guibitfield "warnings" fullconfilter 0x02
            guibitfield "errors" fullconfilter 0x04
            guibitfield "script messages" fullconfilter 0x20
            guibitfield "chat" fullconfilter 0x100
            guibitfield "team chat" fullconfilter 0x200
            guibitfield "game events" fullconfilter 0x400
            guibitfield "self frags" fullconfilter 0x800
            guibitfield "other player frags" fullconfilter 0x1000
            guibitfield "team kills" fullconfilter 0x2000
        ]
        guispring
      ]
    ]
    guistrut 1
    guilist [
        guitext "top left console size (lines):" 0
        guistrut 1
        guilist [
            guilist [
                guistayopen [ guibutton "-" [consize (+ $consize -1)] ]
                guistrut 1
                guifield consize 2
                guistrut 1
                guistayopen [ guibutton "+" [consize (+ $consize 1)] ]
            ]
        ]
    ]
    guistrut 1
    guicheckbox "chat box (bottom left)" miniconfilter 0x300 0


    guitab "autoexec" ////////////////////////////////////////////////////////////////////////////////////////////////////////////
    showfileeditor "autoexec.cfg" -60 16
] "game"

+fround = [round (+f $arg1 $arg2) (absf $arg2)]

newgui ammobar [
    guilist [
        guilist [
            guitext "style:" 0
            guitext "^f7orientation" 0
            guitext "^f7size" 0
            guitext "^f7icon spacing" 0
            guitext "^f7label size" 0
            guitext "^f7label offset" 0
            guistrut .5
            guitext "position:" 0
            guitext "^f7anchor" 0
            guitext "^f7x" 0
            guitext "^f7y" 0
        ]
        guistrut 2
        guilist [
            guistrut 1
            guilist [
                guicheckbox "horizontal" [ammobarhorizontal]
                guistrut 1
                guicheckbox "flip"       [ammobarflip]
            ]
            guilist [
                guistayopen [ guibutton "-" [ammobarscale (+fround $ammobarscale -0.01)] ]
                guistrut 1
                guifield ammobarscale 5
                guistrut 1
                guistayopen [ guibutton "+" [ammobarscale (+fround $ammobarscale 0.01)] ]
            ]
            guislider ammobarsep
            guilist [
                guistayopen [ guibutton "-" [ammobarcountscale (+fround $ammobarcountscale -0.01)] ]
                guistrut 1
                guifield ammobarcountscale 5
                guistrut 1
                guistayopen [ guibutton "+" [ammobarcountscale (+fround $ammobarcountscale 0.01)] ]
            ]
            guislider ammobarcountsep
            guistrut .5
            guistrut 1
            guilist [
                guiradio "first icon" ammobaralign -1
                guistrut 1
                guiradio "center"     ammobaralign  0
                guistrut 1
                guiradio "last icon"  ammobaralign  1
            ]
            guilist [
                guistayopen [ guibutton "-" [ammobarx (+fround $ammobarx -0.01)] ]
                guistrut 1
                guifield ammobarx 5
                guistrut 1
                guistayopen [ guibutton "+" [ammobarx (+fround $ammobarx 0.01)] ]
            ]
            guilist [
                guistayopen [ guibutton "-" [ammobary (+fround $ammobary -0.01)] ]
                guistrut 1
                guifield ammobary 5
                guistrut 1
                guistayopen [ guibutton "+" [ammobary (+fround $ammobary 0.01)] ]
            ]
        ]
    ]

    guistrut .5

    guicheckbox "hide empty stacks" [ammobarhideempty]
] "ammobar"


newgui gameclock [
    guilist [
        guilist [
            guitext "style:" 0
            guitext "^f7size" 0
            guitext "^f7opacity" 0
            guistrut .5
            guitext "position:" 0
            guitext "^f7anchor" 0
            guitext "^f7x" 0
            guitext "^f7y" 0
        ]
        guistrut 2
        guilist [
            guistrut 1
            guilist [
                guistayopen [ guibutton "-" [gameclockscale (+fround $gameclockscale -0.01)] ]
                guistrut 1
                guifield gameclockscale 5
                guistrut 1
                guistayopen [ guibutton "+" [gameclockscale (+fround $gameclockscale 0.01)] ]
            ]
            guislider gameclockalpha
            guistrut .5
            guistrut 1
            guilist [
                guiradio "start"  gameclockalign -1
                guistrut 1
                guiradio "center" gameclockalign  0
                guistrut 1
                guiradio "end"    gameclockalign  1
            ]
            guilist [
                guistayopen [ guibutton "-" [gameclockx (+fround $gameclockx -0.01)] ]
                guistrut 1
                guifield gameclockx 5
                guistrut 1
                guistayopen [ guibutton "+" [gameclockx (+fround $gameclockx 0.01)] ]
            ]
            guilist [
                guistayopen [ guibutton "-" [gameclocky (+fround $gameclocky -0.01)] ]
                guistrut 1
                guifield gameclocky 5
                guistrut 1
                guistayopen [ guibutton "+" [gameclocky (+fround $gameclocky 0.01)] ]
            ]
        ]
    ]
] "gameclock"


newgui hudscore [
    guilist [
        guilist [
            guitext "style:" 0
            guitext "^f7size" 0
            guitext "^f7opacity" 0
            guitext "^f7spacing" 0
            guistrut .5
            guitext "position:" 0
            guitext "^f7anchor" 0
            guitext "^f7x" 0
            guitext "^f7y" 0
        ]
        guistrut 2
        guilist [
            guistrut 1
            guilist [
                guistayopen [ guibutton "-" [hudscorescale (+fround $hudscorescale -0.01)] ]
                guistrut 1
                guifield hudscorescale 5
                guistrut 1
                guistayopen [ guibutton "+" [hudscorescale (+fround $hudscorescale 0.01)] ]
            ]
            guislider hudscorealpha
            guislider hudscoresep
            guistrut .5
            guistrut 1
            guilist [
                guiradio "start"  hudscorealign -1
                guistrut 1
                guiradio "center" hudscorealign  0
                guistrut 1
                guiradio "end"    hudscorealign  1
            ]
            guilist [
                guistayopen [ guibutton "-" [hudscorex (+fround $hudscorex -0.01)] ]
                guistrut 1
                guifield hudscorex 5
                guistrut 1
                guistayopen [ guibutton "+" [hudscorex (+fround $hudscorex 0.01)] ]
            ]
            guilist [
                guistayopen [ guibutton "-" [hudscorey (+fround $hudscorey -0.01)] ]
                guistrut 1
                guifield hudscorey 5
                guistrut 1
                guistayopen [ guibutton "+"  [hudscorey (+fround $hudscorey 0.01)] ]
            ]
        ]
    ]
] "hudscore"

newgui scoreboard [
    guicheckbox "show at death" deathscore
    guistrut 1
    guitext "columns:" 0
    guicheckbox "flags"    [showflags]
    guicheckbox "frags"    [hidefrags] 0 1
    guicheckbox "deaths"   [showdeaths]
    guicheckbox "suicides" [showsuicides]
    guicheckbox "kpd"      [showkpd]
    guicheckbox "accuracy" [showaccuracy]
    guilist [
        guistayopen [
            guicheckbox "damage" [showdamage]
            if $showdamage [
                guistrut 2
                guiradio "dealt"  showdamage 1
                guistrut 1
                guiradio "net"    showdamage 2
            ]
        ]
    ]
    guicheckbox "damage received" [showdamagereceived]
    guicheckbox "ping"            [showping]
    guicheckbox "pj"              [showpj]
    guicheckbox "cn"              [showclientnum]
] "scoreboard"

newgui hudfragmessages [
    guilist [
        guilist [
            guitext "general:" 0
            guitext "^f7modes" 0
            guitext "^f7max" 0
            guitext "^f7duration" 0
            guitext "^f7types" 0
            guistrut 2
            guistrut .5
            guitext "style:" 0
            guitext "^f7size" 0
            guitext "^f7fade out" 0
            guistrut .5
            guitext "position:" 0
            guitext "^f7x" 0
            guitext "^f7y" 0
            guitext "^f7stack dir" 0
        ]
        guistrut 2
        guilist [
            guistrut 1 // general
            guicheckbox "hide in insta" [hudfragmessages] 2 1
            guislider maxhudfragmessages
            guilist [
                guistayopen [ guibutton "-" [hudfragmessageduration (+fround $hudfragmessageduration -0.1)] ]
                guistrut 1
                guifield hudfragmessageduration 3
                guitext "seconds" 0
                guistrut 1
                guistayopen [ guibutton "+" [hudfragmessageduration (+fround $hudfragmessageduration 0.1)] ]
            ]
            guibitfield "self frags"         hudfragmessagefilter 0x800
            guibitfield "other player frags" hudfragmessagefilter 0x1000
            guibitfield "team kills"         hudfragmessagefilter 0x2000
            guistrut .5
            guistrut 1 // style
            guilist [
                guistayopen [ guibutton "-" [hudfragmessagescale (+fround $hudfragmessagescale -0.1)] ]
                guistrut 1
                guifield hudfragmessagescale 5
                guistrut 1
                guistayopen [ guibutton "+" [hudfragmessagescale (+fround $hudfragmessagescale 0.1)] ]
            ]
            guicheckbox "" [hudfragmessagefade]
            guistrut .5
            guistrut 1 // position
            guilist [
                guistayopen [ guibutton "-" [hudfragmessagex (+fround $hudfragmessagex -0.01)] ]
                guistrut 1
                guifield hudfragmessagex 5
                guistrut 1
                guistayopen [ guibutton "+" [hudfragmessagex (+fround $hudfragmessagex 0.01)] ]
            ]
            guilist [
                guistayopen [ guibutton "-" [hudfragmessagey (+fround $hudfragmessagey -0.01)] ]
                guistrut 1
                guifield hudfragmessagey 5
                guistrut 1
                guistayopen [ guibutton "+" [hudfragmessagey (+fround $hudfragmessagey 0.01)] ]
            ]
            guilist [
                guiradio "up"   hudfragmessagestackdir -1
                guistrut 1
                guiradio "auto" hudfragmessagestackdir  0
                guistrut 1
                guiradio "down" hudfragmessagestackdir  1
            ]
        ]
    ]
] "hud frag messages"

newgui "p1xbratenusage" [
    local players
    players = (listclients 0 0 0)
    guilist [
        guilist [
            looplist cn $players [
                guitext (concatword "^f7" (getclientname $cn)) 0
            ]
        ]
        guistrut 1
        guilist [
            looplist cn $players [
                guitext (concatword "^f7" (getclientp1xbratenversion $cn)) 0
            ]
        ]
    ]
] "p1xbraten usage"

newgui "trailcolors" [

    local _shotgun_red _shotgun_green _shotgun_blue
    _shotgun_red   = (& (>> $trailcolorshotgun 16) 0xFF)
    _shotgun_green = (& (>> $trailcolorshotgun  8) 0xFF)
    _shotgun_blue  = (&     $trailcolorshotgun     0xFF)

    guilist [
        guispring
        guicolor $trailcolorshotgun
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _shotgun_red   0 255 [ trailcolorshotgun $_shotgun_red $_shotgun_green $_shotgun_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _shotgun_green 0 255 [ trailcolorshotgun $_shotgun_red $_shotgun_green $_shotgun_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _shotgun_blue  0 255 [ trailcolorshotgun $_shotgun_red $_shotgun_green $_shotgun_blue ]

    guitab chaingun /////////////////

    local _chaingun_red _chaingun_green _chaingun_blue
    _chaingun_red   = (& (>> $trailcolorchaingun 16) 0xFF)
    _chaingun_green = (& (>> $trailcolorchaingun  8) 0xFF)
    _chaingun_blue  = (&     $trailcolorchaingun     0xFF)

    guilist [
        guispring
        guicolor $trailcolorchaingun
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _chaingun_red   0 255 [ trailcolorchaingun $_chaingun_red $_chaingun_green $_chaingun_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _chaingun_green 0 255 [ trailcolorchaingun $_chaingun_red $_chaingun_green $_chaingun_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _chaingun_blue  0 255 [ trailcolorchaingun $_chaingun_red $_chaingun_green $_chaingun_blue ]

    guitab rocketlauncher /////////////////

    local _rocketlauncher_red _rocketlauncher_green _rocketlauncher_blue
    _rocketlauncher_red   = (& (>> $trailcolorrocketlauncher 16) 0xFF)
    _rocketlauncher_green = (& (>> $trailcolorrocketlauncher  8) 0xFF)
    _rocketlauncher_blue  = (&     $trailcolorrocketlauncher     0xFF)

    guilist [
        guispring
        guicolor $trailcolorrocketlauncher
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _rocketlauncher_red   0 255 [ trailcolorrocketlauncher $_rocketlauncher_red $_rocketlauncher_green $_rocketlauncher_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _rocketlauncher_green 0 255 [ trailcolorrocketlauncher $_rocketlauncher_red $_rocketlauncher_green $_rocketlauncher_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _rocketlauncher_blue  0 255 [ trailcolorrocketlauncher $_rocketlauncher_red $_rocketlauncher_green $_rocketlauncher_blue ]

    guitab rifle /////////////////

    local _rifle_red _rifle_green _rifle_blue
    _rifle_red   = (& (>> $trailcolorrifle 16) 0xFF)
    _rifle_green = (& (>> $trailcolorrifle  8) 0xFF)
    _rifle_blue  = (&     $trailcolorrifle     0xFF)

    guilist [
        guispring
        guicolor $trailcolorrifle
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _rifle_red   0 255 [ trailcolorrifle $_rifle_red $_rifle_green $_rifle_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _rifle_green 0 255 [ trailcolorrifle $_rifle_red $_rifle_green $_rifle_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _rifle_blue  0 255 [ trailcolorrifle $_rifle_red $_rifle_green $_rifle_blue ]

    guitab grenadelauncher /////////////////

    local _grenadelauncher_red _grenadelauncher_green _grenadelauncher_blue
    _grenadelauncher_red   = (& (>> $trailcolorgrenadelauncher 16) 0xFF)
    _grenadelauncher_green = (& (>> $trailcolorgrenadelauncher  8) 0xFF)
    _grenadelauncher_blue  = (&     $trailcolorgrenadelauncher     0xFF)

    guilist [
        guispring
        guicolor $trailcolorgrenadelauncher
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _grenadelauncher_red   0 255 [ trailcolorgrenadelauncher $_grenadelauncher_red $_grenadelauncher_green $_grenadelauncher_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _grenadelauncher_green 0 255 [ trailcolorgrenadelauncher $_grenadelauncher_red $_grenadelauncher_green $_grenadelauncher_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _grenadelauncher_blue  0 255 [ trailcolorgrenadelauncher $_grenadelauncher_red $_grenadelauncher_green $_grenadelauncher_blue ]

    guitab pistol /////////////////

    local _pistol_red _pistol_green _pistol_blue
    _pistol_red   = (& (>> $trailcolorpistol 16) 0xFF)
    _pistol_green = (& (>> $trailcolorpistol  8) 0xFF)
    _pistol_blue  = (&     $trailcolorpistol     0xFF)

    guilist [
        guispring
        guicolor $trailcolorpistol
        guispring
    ]
    guistrut 1
    guitext "^f3red"   0
    guislider _pistol_red   0 255 [ trailcolorpistol $_pistol_red $_pistol_green $_pistol_blue ]
    guistrut 1
    guitext "^f0green" 0
    guislider _pistol_green 0 255 [ trailcolorpistol $_pistol_red $_pistol_green $_pistol_blue ]
    guistrut 1
    guitext "^f1blue"  0
    guislider _pistol_blue  0 255 [ trailcolorpistol $_pistol_red $_pistol_green $_pistol_blue ]

] "shotgun"

bindactions = [forward backward left right jump attack togglezoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon" "universaldelta 1" "universaldelta -1" dropflag addbot delbot]

entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
  entguitype = (enttype)
  @(loopconcat i 5 [result [
    entguiattr@i = (entattr @i)
  ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ] [
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

guilistsplit = [
  guilist [
    z = (div (+ (listlen $arg3) (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        if (>= $numargs 6) [arg6]
        loopsublist $arg1 $arg3 (* $a $z) $z [arg4]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "quick light"       [calclight -1]
  guibutton "optimize map"      [remip]
  guibutton "new entity"        [newent shells]
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        [editmat air]
    guibutton "water"                      [editmat water]
    guibutton "lava"                       [editmat lava]
    guibutton "clip"                       [editmat clip]
    guibutton "glass"                      [editmat glass]
    guibutton "noclip"                     [editmat noclip]
    guibutton "gameclip"                   [editmat gameclip]
    guibutton "death"                      [editmat death]
    guibutton "alpha"                      [editmat alpha]
    guicheckbox "show material volumes"    [showmat]
]

brushmenu = [
    guilist [ // diamonds
        guiimage "packages/icons/brush_1c.png"   [brush_0]
        guiimage "packages/icons/brush_21c.png"  [brush_1]
        guiimage "packages/icons/brush_421c.png" [brush_2]
    ]
    guilist [ // squares
        guiimage "packages/icons/brush_3s.png"   [brush_3]
        guiimage "packages/icons/brush_5s.png"   [brush_4]
        guiimage "packages/icons/brush_7s.png"   [brush_5]
    ]
    guititle smooth
    guilist [ // smooth
        guiimage "packages/icons/brush_3s.png"   [brush_6]
        guiimage "packages/icons/brush_5s.png"   [brush_7]
        guiimage "packages/icons/brush_7s.png"   [brush_8]
    ]
]

newentgui = [
  genentattributes $arg1 $arg2 $arg3
  newgui $arg1 [
    guitext $entguitype
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab misc
    @quickeditmenu
  ] "" [initentgui]
]

looplist i $enttypelist [
  newentgui $i "" ""
]

newgui "materials" [
    @matmenu
    guitab misc
    @quickeditmenu
]

newgui "brushes" [
    @brushmenu
    guitab misc
    @quickeditmenu
]

newgui "quickedit" [
    @quickeditmenu
    guitab materials
    @matmenu
]

newentgui light "radius red green blue" "1 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 372"
newentgui envmap "radius" "0 512"
newentgui jumppad "Z Y X" "-128 128 -128 127 -128 127"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 13"

showentgui = [ contexteditgui ] // legacy bind
contexteditgui = [
    if $hmapedit [showgui "brushes"] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui (? (havesel) materials quickedit)]
    ]
]

newgui "seltexinfo" [
    guitext (concatword "slot: " (getseltex))
    guilist [ loop x 5 [
        texname = (gettexname (getseltex) $x)
        texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
        guiimage (concatword "packages/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]
